package parser;

import java_cup.runtime.*;   // Library needed for Java-CUP for parsing
import java.util.List;
import java.util.ArrayList;

import source.*;
import tree.Tree;
import tree.DeclNode;
import tree.ConstExp;
import tree.StatementNode;
import tree.ExpNode;
import tree.Operator;
import syms.*;

/** PL0 definition for CUP LALR(1) parser generator.
 * @version $Revision: 22 $Date: 2013-05-08 13:58:21 +1000 (Wed, 08 May 2013)$
 * This module is responsible for parsing the input file in terms
 * of the tokens supplied by the lexical analyser (see PL0.flex),
 * and generating a symbol table skeleton and an abstract syntax 
 * tree for program.
 
 * Java-CUP will output a file "CUPParser.java" that contains the parser
 * for PL0. It also outputs CUPToken.java.
 * The name "CUPParser.java" is determined by the "parser" option and
 * the name "CUPToken.java" by the "symbols" option to the java_cup command 
 * line or to the corresponding ant task.
 */

parser code 
{:  /* This section provides some methods used by Java_CUP during parsing.
       They override its default methods for reporting syntax errors. */
        
    /** Retrieve the error handler to handle error messages. */
    private Errors errors = ErrorHandler.getErrorHandler();
    
    /** Override the default CUP syntax_error method with one 
     * that integrates better with the compiler's error reporting. */
    @Override
    public void syntax_error( Symbol cur_token ) {
        errors.error( "PL0 syntax error", new Position( cur_token.left ) );
    }
    /** Override the default CUP unrecovered_syntax_error method with one 
     * that integrates better with the compiler's error reporting. */
    @Override
    public void unrecovered_syntax_error( Symbol cur_token ) {
        errors.error( "PL0 unrecovered syntax error", 
                        new Position( cur_token.left ) );
    }
            
:}

action code 
{:  /* This section provides global variables and methods used in the
     * semantics actions associated with parsing rules.
     * These are the only global variables you should need. */

    /** Error handler for reporting error messages. */
    private Errors errors = ErrorHandler.getErrorHandler();
    
    /** Symbol table is available globally.
     *  Its current scope corresponds to the procedure/main program 
     *  being processed.
     */
    SymbolTable symtab;
:}


/*--------------------------------------------------------------*
 *--------------------- TERMINAL SYMBOLS -----------------------*
 *--------------------------------------------------------------*
 * CUP generates a file CUPToken.java containing the Java definitions 
 * generated by from the following list of terminal symbols.
 * CUPToken.java is used to communicate with the lexical analyser. 
 * The name "CUPToken.java" is determined by the "symbols" option to java_cup
 * (specified in command line invoking java-cup or in the corresponding ant 
 * task).
 * See PL0.flex for the detailed definitions of the lexical tokens.
 */
terminal
    LPAREN,       /* ( */
    RPAREN,       /* ) */
    SEMICOLON,    /* ; */
    COLON,        /* : */
    ASSIGN,       /* := */
    PLUS,         /* + */
    MINUS,        /* - */
    TIMES,        /* * */
    DIVIDE,       /* / */
    EQUALS,       /* = */
    NEQUALS,      /* != */
    LEQUALS,      /* <= */
    GEQUALS,      /* >= */
    LESS,         /* < */
    GREATER,      /* > */
    RANGE,        /* .. */
    PERIOD,       /* . */
    COMMA,        /* , */
    POINTER,      /* ^ */
    LCURLY,       /* { */
    RCURLY,       /* } */
    LBRACKET,     /* [ */
    RBRACKET,     /* ] */
    KW_BEGIN,     /* begin */
    KW_CALL,      /* call */
    KW_CONST,     /* const */
    KW_DO,        /* do */
    KW_ELSE,      /* else */
    KW_END,       /* end */
    KW_IF,        /* if */
    KW_NEW,       /* new */
    KW_PROCEDURE, /* procedure */
    KW_READ,      /* read */
    KW_RECORD,    /* record */
    KW_THEN,      /* then */
    KW_TYPE,      /* type */
    KW_VAR,       /* var */
    KW_WHILE,     /* while */
    KW_WRITE,     /* write */
    ILLEGAL;      /* illegal symbol - not used in grammar */

/** The following terminal symbols have an attribute associated with them.
 */
terminal String  IDENTIFIER;   /* identifier */
terminal Integer NUMBER;       /* number */

/**-------------------------------------------------------------*
 *------------------ NONTERMINAL SYMBOLS -----------------------*
 *--------------------------------------------------------------*
 * Many nonterminals (but not all) have an attribute type associated 
 * with them. This must be a Java class (subclass of Object, e.g., 
 * Integer rather than int).
 * This gives the type of the special attribute RESULT in the grammar 
 * rules in which the nonterminal appears on the left side of a grammar
 * rule, and the type of the attribute corresponding to the label 
 * attached to occurrences of the nonterminal on the right side of a rule.
 *
 * The attributes are used to construct the symbol table for the
 * declaration parts and the abstract syntax tree for the bodies of
 * procedures and the main program.
 */
non terminal Tree.ProgramNode Program;
non terminal Tree.BlockNode   Block;
non terminal DeclNode.DeclListNode DeclarationList;
non terminal                  Declaration;
non terminal                  ConstDefSeq, 
                              ConstDef;
non terminal ConstExp         Constant;
non terminal                  TypeDefSeq, 
                              TypeDef;
non terminal Type             Type,
                              RecordType,
                              PointerType;
non terminal Type.IdRefType   TypeIdentifier;
non terminal Type.RecordType  FieldList,
							  Fields;
non terminal Type.Field		  Field;
non terminal                  VarDeclSeq, 
                              VarDecl;
non terminal DeclNode.ProcedureNode ProcedureDef;
non terminal Scope            ProcedureHead;
non terminal 				  FormalParamList;
non terminal 				  ActualParamList;
non terminal StatementNode.ListNode StatementList;
non terminal StatementNode    Statement,
                              CompoundStatement;
non terminal ExpNode          Condition,
                              RelCondition,
                              Exp,
                              Term,
                              Factor;
non terminal Operator         Relation,
                              AddOp,
                              MulOp,
                              UnaryOperator;
non terminal ExpNode          LValue;
non terminal List<ExpNode>    RecordFields,
							  ExpList;

/**-------------------------------------------------------------*
 *------------------------ GRAMMAR RULES -----------------------*
 *--------------------------------------------------------------*
 * Grammar productions have actions associated with them, which are
 * executed after recognising (part of) the right side of the production.
 *  
 * The action is Java code contained between '{:' and ':}'
 * It is allowed to reference the values of attributes of symbols on 
 * the right side (the name of these attributes is the name after the 
 * ':' following a symbol, e.g., in the rule for Program the attribute 
 * associated with Block is 'block'), and assignments to the attribute of 
 * the left side nonterminal (which is always referred to as 'RESULT').
 *
 * Be careful to get the syntax of the '{:' and ':}' correct and to
 * make sure they match up, otherwise the error messages get very
 * confusing. Also don't forget the ";" at the end of a production.
 * 
 * To allow the positions of errors in the input source program to be
 * reported, each attribute, such as "id" associated with IDENTIFIER
 * in the production for ConstDef, has two further attributes "idleft" 
 * and "idright", representing the positions (think of a position as
 * a line and column number) of the leftmost and rightmost characters 
 * of the construct in the input source program.
 * Note that the name "idleft" is constructed from the name "id" by
 * adding "left" to it. It is a character offset from the start of the
 * source file, but we encapsulate it as a Position as soon as possible.
 *
 * Java-CUP has a special non-terminal symbol 'error', which is used to 
 * allow recovery from syntax errors in the program. 
 *
 * The action code usually comes at the end of the right side of a production,
 * however, it is possible to have multiple actions, each of which is executed
 * as soon as the symbols preceding it are matched.
 * For example, for Program the first thing on the right side is an 
 * action, and hence this action is executed before the right side of
 * program (namely Block) is recognised, i.e., before any of the 
 * Program is recognised. The second action following Block is executed
 * after the whole of the Block representing the program is recognised.
 */
Program ::=  /* empty */ 
        {:
            /* This action occurs before the whole program is recognised.
             * Construct initial symbol table with current scope the 
             * predefined scope. */
            symtab = new SymbolTable(); 
            /* Set up a dummy symbol table entry for the main program */
            SymEntry.ProcedureEntry main = 
                new SymEntry.ProcedureEntry("<main>", Position.NO_POSITION, 
                        symtab.getCurrentScope() );
            /* Enter the scope for the main program and save the new local
             * scope in main's symbol table entry */
            main.setLocalScope( symtab.newScope( main ) );
        :}
     Block:block
        {: 
            /* This action is executed after the whole program 
             * has been recognised */
            block.setBlockLocals( symtab.getCurrentScope() );
            symtab.leaveScope();
            /* Returned result of the whole parsing phase */
            RESULT = new Tree.ProgramNode( new Position( blockright), 
                        symtab, block );
        :}
    | error
        {:
            /* A null result from Program will cause the compiler to
               avoid further processing. */
            RESULT = null;
        :}
    ;
Block ::= DeclarationList:dl CompoundStatement:b
        {: 
            RESULT = new Tree.BlockNode( new Position( bright ), dl, b );
        :}
    ;
DeclarationList ::= DeclarationList:dl ProcedureDef:p SEMICOLON
        {:
            dl.addDeclaration(p);
            RESULT = dl;
        :}
    | DeclarationList:dl Declaration 
        {: 
            RESULT = dl;
        :}
    | /* empty */
        {:
            RESULT = new DeclNode.DeclListNode();
        :}
    ;

Declaration ::= KW_CONST ConstDefSeq
    |  KW_TYPE TypeDefSeq
    |  KW_VAR  VarDeclSeq
    ;

ConstDefSeq ::= ConstDef
    |  ConstDefSeq ConstDef
    ;

ConstDef ::= IDENTIFIER:id EQUALS Constant:c SEMICOLON
        {: 
            /* The attribute idleft represents the position of the start
             * character of the IDENTIFIER token in the input stream.
             * The class Position wraps this to provide a level of abstraction
             * for positions in the input stream.
             */
            Position pos = new Position(idleft);
            /* addConstant returns null if id is already defined 
             * in the current scope */
            if( symtab.addConstant( id, pos, c ) == null ) {
                errors.error( id + " already defined", pos );
            }
        :}
    |  error 
    ;
/* The rules for Constant construct a (mini) abstract syntax tree 
 * for constant expressions (not to be confused with ExpNodes).
 */
Constant ::= NUMBER:n
        {: 
            RESULT = new ConstExp.NumberNode( new Position( nleft), 
                    symtab.getCurrentScope(), Predefined.INTEGER_TYPE, n ); 
        :}
    | MINUS:op Constant:c
        {: 
            RESULT = new ConstExp.NegateNode( new Position( opleft ), 
                    symtab.getCurrentScope(), c ); 
        :}
    |  IDENTIFIER:id
        {: 
            RESULT = new ConstExp.ConstIdNode( new Position( idleft ), 
                    symtab.getCurrentScope(), id );
        :}
    |  error:err
        {:
            RESULT = new ConstExp.ErrorNode( new Position( errleft ),
                                             symtab.getCurrentScope() );
        :}
    ;

TypeDefSeq ::= TypeDef 
    |  TypeDefSeq TypeDef
    ;

TypeDef ::= IDENTIFIER:id EQUALS Type:type SEMICOLON
        {: 
            Position pos = new Position(idleft);
            SymEntry.TypeEntry typeEntry = symtab.addType( id, pos, type );
            if( typeEntry == null ) {
                errors.error( id + " already defined", pos );
            } else {
                type.setName( id ); // for error messages
            }
        :}
    |  error 
    ;

Type ::= TypeIdentifier:type
        {: 
            RESULT = type;
        :}
    | LBRACKET Constant:lo RANGE Constant:hi RBRACKET
        {:
            RESULT = new Type.SubrangeType( lo, hi );
        :}
    | PointerType:type
    	{:
    		RESULT = type;
    	:}
    | RecordType:type
    	{:
    		RESULT = type;
    	:}
    | error:err
        {:
            RESULT = Type.ERROR_TYPE;
        :}
    ;
    
TypeIdentifier ::= IDENTIFIER:id
        {: /* As the type identifier may not be defined at this point
            * the abstract syntax tree records the id, as well as the 
            * symbol table context to look it up within later.
            */
            RESULT = new Type.IdRefType( id, symtab.getCurrentScope(),
                                         new Position( idleft ) );
        :}
    ;
    
PointerType ::= POINTER TypeIdentifier:type
		{:
			Type.PointerType resultType = new Type.PointerType(type);
			Type.ProductType pairPointerType = new Type.ProductType(resultType, resultType);
			Type.FunctionType pointerRelational = new Type.FunctionType(pairPointerType, Predefined.BOOLEAN_TYPE);
    		symtab.addOperator("_=_", Position.NO_POSITION, pointerRelational );
    		symtab.addOperator("_!=_", Position.NO_POSITION, pointerRelational );
			RESULT = resultType;
		:}
	;
	
RecordType ::= KW_RECORD FieldList:fieldList KW_END
		{:
			RESULT = fieldList;
		:}
	;
	
FieldList ::= Fields:fields
		{:
			RESULT = fields;
		:}
	|	
		{:
			// return empty record
			RESULT = new Type.RecordType();
		:}
	; 
	
Fields ::= Field:field
		{:
			RESULT = new Type.RecordType();
			RESULT.add(field);
		:}
	| Fields:fields SEMICOLON Field:field
		{:
			if( ! fields.containsField(field.getId()) ) {
				fields.add(field);
			} else { // repeated field name
				errors.error(field.getId() + " repeated field name", field.getPosn());
			}
			RESULT = fields;
		:}
	;
	
Field ::= IDENTIFIER:id COLON TypeIdentifier:type
		{:
			Position pos = new Position(idleft);
			RESULT = new Type.Field(pos, id, type);
		:}	
	;
	
VarDeclSeq ::= VarDecl
    |  VarDeclSeq VarDecl
    ;

VarDecl ::= IDENTIFIER:id COLON TypeIdentifier:type SEMICOLON
        {: 
            Position pos = new Position(idleft);
            // Variables are always of ReferenceType.
            Type.ReferenceType varType = new Type.ReferenceType( type );
            if( symtab.addVariable( id, pos, varType ) == null ) {
                errors.error( id + " already declared", pos );
            }
        :}
    |  error
    ;
    
ProcedureDef ::= ProcedureHead:scope EQUALS Block:b
        {:
            b.setBlockLocals( scope );
            symtab.leaveScope();
            RESULT = new DeclNode.ProcedureNode( scope.getProcEntry(), b );
        :}
    ;

ProcedureHead ::= KW_PROCEDURE IDENTIFIER:id
            LPAREN FormalParamList:pl RPAREN 
        {: 
            SymEntry.ProcedureEntry procEntry;
            /* Add an entry for the procedure to the current scope */
            Position pos = new Position(idleft);
            procEntry = symtab.addProcedure( id, pos );
            if( procEntry == null ) {
                errors.error( id + " already declared", pos );
                /* Create a dummy symbol table entry.
                 * Note that this entry isn't in symtab. */
                procEntry = new SymEntry.ProcedureEntry( id, 
                    pos, symtab.getCurrentScope() );
            }
            /* Create a new scope for the symbols local to
             * the procedure. */
            RESULT = symtab.newScope( procEntry );
            procEntry.setLocalScope( RESULT );
        :}
    |  KW_PROCEDURE IDENTIFIER:id error
        {: 
            SymEntry.ProcedureEntry procEntry = 
                new SymEntry.ProcedureEntry( id, new Position( idleft ),
                            symtab.getCurrentScope() );
            RESULT = symtab.newScope( procEntry );
            procEntry.setLocalScope( RESULT );
        :}
    ;


/* To allow for the addition of procedure parameters. */
FormalParamList ::= /* empty */
    ;

CompoundStatement ::= KW_BEGIN StatementList:sl KW_END
        {:
            RESULT = sl;
        :}
    ;

StatementList ::= Statement:s
        {:
            RESULT = new StatementNode.ListNode( new Position(sleft) );
            RESULT.addStatement(s);
        :}
    |  StatementList:sl SEMICOLON Statement:s
        {:
            sl.addStatement( s );
            RESULT = sl;
        :}
    ;

Statement ::= KW_WHILE Condition:c KW_DO Statement:s
        {: 
            RESULT = new StatementNode.WhileNode( new Position(cleft), c, s );
        :}
    |  KW_IF Condition:c KW_THEN Statement:s1 KW_ELSE Statement:s2
        {: 
            RESULT = new StatementNode.IfNode( new Position(cleft), c, s1, s2 );
        :}
    |  CompoundStatement:s
        {: 
            RESULT = s; 
        :}
    | KW_READ:r LValue:lval
        {: 
            RESULT = new StatementNode.AssignmentNode( new Position(lvalleft), 
                     lval, new ExpNode.ReadNode( new Position(rleft) ) );
        :}
    |  KW_WRITE Exp:e
        {: 
            RESULT = new StatementNode.WriteNode( new Position(eleft), e );
        :}
    | LValue:lval ASSIGN Condition:rval
       {:
          RESULT = new StatementNode.AssignmentNode( new Position(lvalleft),
                                            lval, rval );
       :} 
    |  KW_CALL IDENTIFIER:id 
            LPAREN ActualParamList:pl RPAREN
        {: 
            RESULT = new StatementNode.CallNode( new Position(idleft), id );
        :}
    |  error:p 
        {: 
            RESULT = new StatementNode.ErrorNode( new Position(pleft) );
        :}
    ;

ActualParamList ::= /* empty */
    ;

LValue ::= IDENTIFIER:id
        {:
            RESULT = new ExpNode.IdentifierNode( new Position(idleft), id );
        :}
	|  LValue:lvalue PERIOD:p IDENTIFIER:id
		{:
			ExpNode.IdentifierNode field = new ExpNode.IdentifierNode( new Position(idleft), id );
			RESULT = new ExpNode.FieldAccessNode( new Position(lvalueleft), new Position(pleft), lvalue, field );
		:}
	|  LValue:lvalue POINTER
		{:
			RESULT = new ExpNode.PointerDereferenceNode( new Position(lvalueleft), lvalue );
		:}      
    ;

/* To allow for adding logical expressions. */
Condition ::= RelCondition:e
        {: 
            RESULT = e;
        :}
    ;
/* Relational operators are lower precedence than arithmetic operators. */
RelCondition ::= Exp:e 
        {:
            RESULT = e;
        :}
    |  Exp:e1 Relation:op Exp:e2
        {:
            RESULT = new ExpNode.OperatorNode( new Position( opleft ),
                op, new ExpNode.ArgumentsNode(e1,e2) );
        :} 
    ;

Relation ::= EQUALS
        {:
            RESULT = Operator.EQUALS_OP;
        :}
    |  NEQUALS
        {:
            RESULT = Operator.NEQUALS_OP;
        :}
    |  LEQUALS
        {:
            RESULT = Operator.LEQUALS_OP;
        :}
    |  LESS
        {:
            RESULT = Operator.LESS_OP;
        :}
    |  GREATER
        {:
            RESULT = Operator.GREATER_OP;
        :}
    |  GEQUALS
        {:
            RESULT = Operator.GEQUALS_OP;
        :}
    ;
/* Expressions are structured into Exp, Term, and Factor to encode the
 * precedence of the operators in the grammar.
 */
Exp ::= Term:t
        {:
            RESULT = t;
        :}
    |  Exp:e1 AddOp:op Term:e2
        {:
            RESULT = new ExpNode.OperatorNode(new Position(opleft),op,
                new ExpNode.ArgumentsNode(e1,e2) );
        :}
    ;

AddOp ::= PLUS
        {:
            RESULT = Operator.ADD_OP;
        :}
    |  MINUS
        {:
            RESULT = Operator.SUB_OP;
        :}
    ;

Term ::= Factor:f
        {:
            RESULT = f;
        :}
    |  Term:e1 MulOp:op Factor:e2
        {:
            RESULT = new ExpNode.OperatorNode(new Position(opleft),op,
                new ExpNode.ArgumentsNode(e1,e2) );
        :}
    ;

MulOp ::= TIMES
        {:
            RESULT = Operator.MUL_OP;
        :}
    |  DIVIDE
        {:
            RESULT = Operator.DIV_OP;
        :}
    ;

Factor ::= PLUS Factor:e
        {:
            RESULT = e; 
        :}
    |  UnaryOperator:op Factor:e
        {:
            RESULT = new ExpNode.OperatorNode( new Position( opleft ), 
                        op, e );
        :}
    |  LPAREN Condition:c RPAREN
        {:
            RESULT = c;
        :}
    |  NUMBER:n
        {:
            RESULT = new ExpNode.ConstNode( new Position( nleft ), 
                    Predefined.INTEGER_TYPE, n.intValue() );
        :}
    |  LValue:lval
        {:
            RESULT = lval;
        :}
   	|  KW_NEW TypeIdentifier:recordType
   		{:
   			Position pos = new Position(recordTypeleft);
   			RESULT = new ExpNode.NewExpNode( pos, recordType );
   		:}
   	|  TypeIdentifier:recordType LCURLY RecordFields:recordFields RCURLY
   		{:
   			Position pos = new Position(recordTypeleft);
   			RESULT = new ExpNode.RecordConstructorNode( pos, recordType, recordFields );
   		:}
    | error:e
        {:
            RESULT = new ExpNode.ErrorNode( new Position(eleft) );
        :}
    ;
    
RecordFields ::= ExpList:expList
		{:
			RESULT = expList;
		:}    
    |
    	{:
    		RESULT = new ArrayList<ExpNode>();
    	:}
    ;
    
ExpList ::= Condition:condition
		{:
			RESULT = new ArrayList<ExpNode>();
			RESULT.add(condition);
		:}    
    |  ExpList:expList COMMA Condition:condition
    	{:
    		expList.add(condition);
    		RESULT = expList;
    	:}
    ;
    
UnaryOperator ::= MINUS:op
        {:
            RESULT = Operator.NEG_OP;
        :}
    ;

